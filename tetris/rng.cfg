// This files makes generating random numbers in console possible

// import utils
exec tetris/utils

// Gcc numbers according do wikipedia
svar_set lcg_a 1103515
svar_set lcg_b 12345
svar_set lcg_m 42949672

// Grab tick count to init the seed (mlugg :heart_eyes:)
// Return variable: tick_ret
sar_function tick "svar_capture tick_tmp sar_session; tick_h1"
sar_function tick_h1 tick_h2 $tick_tmp
sar_function tick_h2 svar_set tick_ret $4

// Init the lcg state with the seed
sar_function init_seed "svar_set lcg_state $tick_ret"
tick
init_seed

// Advance the lcg internal state
sar_function lcg_advance_state "svar_mul lcg_state lcg_a; svar_add lcg_state lcg_b; svar_mod lcg_state lcg_m; lcg_advance_state_h1; lcg_advance_state_h2"
sar_function lcg_advance_state_h1 "neg $lcg_state"
sar_function lcg_advance_state_h2 cond "var:neg_ret=1" "svar_add lcg_state i32_max"

// Make a brand-new random number that is less that $1
// This performs lcg_state <- (lcg_a*lcg_state+lcg_b)%lcg_m
// Return variable: rand_ret
sar_function rand "lcg_advance_state; svar_set rand_ret $lcg_state; svar_mod rand_ret $1"

// Refill the current bag
sar_function refill_bag "svar_set current_bag_size 7; svar_set current_bag_0 I; svar_set current_bag_1 T; svar_set current_bag_2 O; svar_set current_bag_3 L; svar_set current_bag_4 J; svar_set current_bag_5 S; svar_set current_bag_6 Z; svar_set current_bag_7 E"
refill_bag

// Removes the $1 piece from the bag and returns it
// Return variable: remove_piece_from_bag_ret
sar_function remove_piece_from_bag "remove_piece_from_bag_h; svar_append remove_piece_from_bag_ret current_bag_$1; svar_set bag_idx $1; remove_piece_from_bag_loop; svar_sub current_bag_size 1"
sar_function remove_piece_from_bag_h svar_set remove_piece_from_bag_ret ""
sar_function remove_piece_from_bag_loop cond "!var:bag_idx=$current_bag_size" "remove_piece_from_bag_loop_h1; remove_piece_from_bag_loop_h2; remove_piece_from_bag_loop_h3; remove_piece_from_bag_loop_h4; svar_add bag_idx 1; remove_piece_from_bag_loop"
sar_function remove_piece_from_bag_loop_h1 svar_set bag_plus_one ""
sar_function remove_piece_from_bag_loop_h2 "svar_set bag_tmp $bag_idx; svar_add bag_tmp 1"
sar_function remove_piece_from_bag_loop_h3 "svar_append bag_plus_one current_bag_$bag_tmp"
sar_function remove_piece_from_bag_loop_h4 "svar_set current_bag_$bag_idx $bag_plus_one"

// Get a piece from the bag
// Return value: rand_piece_ret
sar_function rand_piece "rand $current_bag_size; rand_piece_h1; rand_piece_h2; rand_piece_h3"
sar_function rand_piece_h1 "remove_piece_from_bag $rand_ret"
sar_function rand_piece_h2 "svar_set rand_piece_ret $remove_piece_from_bag_ret"
sar_function rand_piece_h3 cond "var:current_bag_size=0" "refill_bag"

